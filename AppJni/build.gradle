apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.yc.jnidemo"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    //列出所有包含有so文件的库信息，主要是找到项目中so库在那个项目文件夹下
    tasks.whenTaskAdded { task ->
        //如果是有多个flavor，则用 mergeFlavorDebugNativeLibs的形式
        if (task.name=='mergeDebugNativeLibs') {
            task.doFirst {
                println("------------------- find so files start -------------------")
                println("------------------- find so files start -------------------")
                println("------------------- find so files start -------------------")
                it.inputs.files.each { file ->
                    printDir(new File(file.absolutePath))
                }
                println("------------------- find so files end -------------------")
                println("------------------- find so files end -------------------")
                println("------------------- find so files end -------------------")
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "com.google.android:flexbox:0.3.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation project(':CallJniLib')
    implementation project(':TestJniLib')
    implementation project(':SafetyJniLib')
    implementation project(':SignalHooker')
    implementation project(':CrashDumper')
    //implementation project(':EpicJniLib')
}

def printDir(File file) {
    if (file != null) {
        if (file.isDirectory()) {
            file.listFiles().each {
                printDir(it)
            }
        } else if (file.absolutePath.endsWith(".so")) {
            println "find so file: $file.absolutePath"
        }
    }
}
